// XPTS419 - 2015.06.12: Prepare a FileTree with JRE 6 JARs.
// XPTS419 - 2015.06.12: The target version of the code must be Java 6, and not Java 7 or later.
task legacyJRE( description : 'Collects JARs to set as bootstrap classes before compiling.' ) {
  def files = []
  if ( project.hasProperty( 'legacy_jre' ) ) {
    def tree  = fileTree( dir: legacy_jre , include: '**/*.jar' )
    tree.each { File file ->
      files << file
    }
  }

  ext.bootClasspath = files.join( ';' )
}

apply plugin: 'base'
apply plugin: 'java'
apply plugin: 'war'

sourceSets {
  main {
    java {
      srcDir sources_folder
    }
    resources {
      srcDir resources_folder
    }
  }
}

clean {
  delete distribution_folder
}

sourceCompatibility = source_version
targetCompatibility = target_version

dependencies {
  providedCompile fileTree( dir: vendors_folder + '/javax/jaxrs', include: '*.jar' )
}

compileJava {
  classpath            = configurations.compile
  options.compilerArgs = [ '-Xlint:unchecked' ]
  options.encoding     = 'UTF-8'
  options.incremental  = true
  options.fork         = true

  if ( sourceCompatibility == '1.6' ) {
    options.bootClasspath = legacyJRE.bootClasspath
  }
}

war {
  archiveName    = application_file + '.' + extension
  destinationDir = file( distribution_folder )
  manifest {
    attributes ( 'Implementation-Title'    : application_name                     ,
                 'Implementation-Version'  : application_version                  ,
                 'Implementation-Vendor'   : application_vendor                   ,
                 'Implementation-Timestamp': new Date()                           ,
                 'Built-Date'              : new Date()                           ,
                 'Built-JDK'               : System.getProperty( 'java.version' ) ,
                 'Built-By'                : System.getProperty( 'user.name'    ) )
  }
}

task( 'package', description: 'Packages all artifacts.', dependsOn: war ) << {
}

defaultTasks ':assemble'
